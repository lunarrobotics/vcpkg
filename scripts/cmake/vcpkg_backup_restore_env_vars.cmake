function(vcpkg_backup_env_variables)
    cmake_parse_arguments(PARSE_ARGV 0 arg "" "" "VARS")
    if(NOT DEFINED arg_VARS)
        message(FATAL_ERROR "VARS must be defined.")
    endif()
    if(DEFINED arg_UNPARSED_ARGUMENTS)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION} was passed extra arguments: ${arg_UNPARSED_ARGUMENTS}")
    endif()

    foreach(envvar IN LISTS arg_VARS)
        list(LENGTH z_vcpkg_env_backup_${envvar} NUM_LEVEL)
        # creates/modifies th list in current scope
        list(APPEND z_vcpkg_env_backup_${envvar} z_vcpkg_env_backup_${envvar}_${NUM_LEVEL})
        # cache the list variable
        set(z_vcpkg_env_backup_${envvar} ${z_vcpkg_env_backup_${envvar}}  CACHE INTERNAL "" FORCE)
        # set current value of envvvar, if envvar is not exists it will be an empty list
        set(z_vcpkg_env_backup_${envvar}_${NUM_LEVEL} $ENV{${envvar}} CACHE INTERNAL "")
    endforeach()
endfunction()

function(vcpkg_restore_env_variables)
    cmake_parse_arguments(PARSE_ARGV 0 arg "" "" "VARS")
    if(NOT DEFINED arg_VARS)
        message(FATAL_ERROR "VARS must be defined.")
    endif()
    if(DEFINED arg_UNPARSED_ARGUMENTS)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION} was passed extra arguments: ${arg_UNPARSED_ARGUMENTS}")
    endif()

    foreach(envvar IN LISTS arg_VARS)
        if(z_vcpkg_env_backup_${envvar})
            list(POP_BACK z_vcpkg_env_backup_${envvar} VARIABLE_TO_BACKUP)
            # make list available globally
            set(z_vcpkg_env_backup_${envvar} ${z_vcpkg_env_backup_${envvar}}  CACHE INTERNAL "" FORCE)
            # if environment variable to backup was not empty, restore it
            if (${VARIABLE_TO_BACKUP})
                set("ENV{${envvar}}" "${${VARIABLE_TO_BACKUP}}")
            else()
                unset("ENV{${envvar}}")
            endif()
            unset(${VARIABLE_TO_BACKUP} CACHE)
            # if list is empty unset it
            if (NOT z_vcpkg_env_backup_${envvar})
                unset(z_vcpkg_env_backup_${envvar} CACHE)
            endif()
        else()
            message(FATAL_ERROR "Unmached vcpkg_restore_env_variables call")
        endif()
    endforeach()
endfunction()
